#include <Arduino.h>
#include <dronecan.h>
#include "Adafruit_MCP9600.h"
#include "Wire.h"

// set up your parameters here with default values. NODEID should be kept
std::vector<DroneCAN::parameter> custom_parameters = {
    {"NODEID", DroneCAN::INT, 100, 0, 127},
    {"DEVICE_ID", DroneCAN::INT, 0, 0, 127},
    {"BATT_EN", DroneCAN::INT, 0, 0, 1},
};

DroneCAN dronecan;

uint32_t loop1time = 0;
uint32_t loop2time = 0;
uint32_t looptime1hz = 0;
int device_id = 0;
int batt_en = 0;

/*
MCP9600 specific setup
*/
#define I2C_ADDRESS (0x66)
Adafruit_MCP9600 mcp;

void setup()
{
    // the following block of code should always run first. Adjust it at your own peril!
    app_setup();
    IWatchdog.begin(2000000);
    Serial.begin(115200);
    dronecan.init(
        custom_parameters,
        "BR-Node-Temperature");
    // end of important starting code

    if (!mcp.begin(I2C_ADDRESS))
    {
        uint32_t deadloop = 0;
        while (1)
        {
            const uint32_t now = millis();
            if (now - deadloop > 1000)
            {
                deadloop = millis();
                dronecan.debug("MCP9600 not found", 0);
                Serial.println("Sensor not found. Check wiring!");
            }
            dronecan.cycle();
            IWatchdog.reload();
        }
    }

    // Set the thermocouple type
    mcp.setThermocoupleType(MCP9600_TYPE_K);
}

void loop()
{
    const uint32_t now = millis();

    if (now - loop1time > 100)
    {
        loop1time = millis();

        if (batt_en)
        {

            uavcan_equipment_power_BatteryInfo pkt{};

            pkt.battery_id = device_id;
            pkt.current = 0;
            pkt.voltage = 0;
            pkt.temperature = mcp.readThermocouple();

            sendUavcanMsg(dronecan.canard, pkt);
        }
    }

    if (now - loop2time > 1000)
    {
        loop2time = millis();

        uavcan_equipment_device_Temperature pkt{};

        pkt.temperature = mcp.readThermocouple();
        pkt.device_id = device_id;

        sendUavcanMsg(dronecan.canard, pkt);
    }

    if (now - looptime1hz > 1000)
    {
        looptime1hz = millis();
        batt_en = dronecan.getParameter("BATT_EN");
        device_id = dronecan.getParameter("DEVICE_ID");
    }

    dronecan.cycle();
    IWatchdog.reload();
}
